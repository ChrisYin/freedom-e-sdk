# The name, version, and maintainer of this package
AC_INIT([freedom-mee], [m4_esyscmd_s([git describe --always])], [palmer@sifive.com])

# Initializes automake, enabling maintainer mode by default (which should be
# disabled by the archive generated by "make dist").
AM_INIT_AUTOMAKE([foreign subdir-objects])
AM_MAINTAINER_MODE([enable])

# Looks up the key toolchain programs that are used when building the MBI,
# such as the C compiler.
AC_PROG_CC
AC_PROG_RANLIB
AM_PROG_AS

# autoconf tries very hard to avoid failing, so it'll even go ahead and try to
# build the project using "gcc" if it can't find a suitable C compiler prefixed
# by the host system.  This doesn't work when cross compiling, but it's a
# common error for users so we explicitly check for this right here to quickly
# provide an error message.
AC_COMPILE_IFELSE([AC_LANG_PROGRAM([],[
#ifndef __riscv
# error "A RISC-V compiler is required to build the Freedom MBI"
#endif
])], [], [AC_MSG_FAILURE([The C compiler doesn't define __riscv, which means it's probably not a RISC-V compiler.  You should specify something like --host=riscv64-sifive-elf when building this, as it will only work on RISC-V systems.])])

# Generates the remainder of the build system.
AC_CONFIG_FILES([Makefile])
AC_OUTPUT
